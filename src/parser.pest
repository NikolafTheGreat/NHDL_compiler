WHITESPACE = _{
    " "|"\t"|"\r"|"\n"
}
COMMENT = _{
    ("/*" ~ (!"*/" ~ ANY)* ~ "*/") |
    ("//" ~ (!"\n" ~ ANY)* ~ "\n") 
}

Top = _{
    SOI ~
    OuterScope ~
    EOI
}

OuterScope = {
    "top" ~ ":" ~ Identifier ~ ";" ~
    Definition*
}

Definition = _{
    ModuleDeclaration|EnumDeclaration|ConstantDeclaration
}

Declaration = _{
    RegisterInstanciation|WireInstanciation|ModuleInstance
}

RegisterInstanciation = {
    "reg" ~ Identifier ~
    "{" ~ "clock" ~ ":" ~ Expression ~
    ("," ~ "reset" ~ ":" ~ Expression ~ 
    "," ~ "initial" ~ ":" ~ Expression )? ~ ","? ~
    "}" ~ ":" ~ Type ~ ";"
}

WireInstanciation = {
    "wire" ~ Identifier ~ ":" ~ Type ~ ";"
}

ModuleInstance = {
    "mod" ~ Identifier ~ ":" ~ Identifier ~ ("<" ~ (Expression ~ ",")* ~ Expression? ~ ">")? ~ ";"
}

//Statements
Statement = _{
    Scope|Connection|If|Switch|Loop|Node
}

Scope = {
    "{" ~ (Statement|Declaration)* ~ "}"
}

ModuleDeclaration = {
    "module" ~ Identifier ~ GenericConstant? ~ IOTypeBundle ~ Statement
}

GenericConstant = {
    "<" ~ (Identifier ~ ":" ~ ConstantType ~ ",")* ~ (Identifier ~ ":" ~ ConstantType)? ~ ">"
}

EnumDeclaration = {
    "enum" ~ Identifier ~ "{" ~ 
        (Identifier ~ ",")* ~ Identifier? ~
    "}"
}

ConstantDeclaration = {
    "const" ~ Identifier ~ ":" ~ ConstantType ~ "=" ~ Expression ~ ";"
}

Connection = _{
    StrongConnection | WeakConnection
}

StrongConnection = {
    Target ~ "<=" ~ (Expression | EnumVariant) ~ ";"
}

WeakConnection = {
    Target ~ "<-" ~ (Expression | EnumVariant) ~ ";"
}

If = {
    "if" ~ "(" ~ Expression ~ ")" ~ Statement ~ ("else" ~ Statement)?
}

Switch = {
    SwitchNumber | SwitchEnum
}

SwitchNumber = _{
    "switch" ~ "(" ~ Expression ~ ")" ~ "{" ~
    (Number ~ ":" ~ Statement ~ ",")* ~
    (
        (Number ~ ":" ~ Statement)|
        (Empty ~ ":" ~ Statement)|
        (Empty ~ ":" ~ Statement ~ ",")
    )? ~

    "}"
}

SwitchEnum = _{
    "switch" ~ "(" ~ Expression ~ ")" ~ "{" ~
    (EnumVariant ~ ":" ~ Statement ~ ",")* ~
    (
        (EnumVariant ~ ":" ~ Statement)|
        (Empty ~ ":" ~ Statement)|
        (Empty ~ ":" ~ Statement ~ ",")
    )? ~

    "}"
}

Empty = {
    "_"
}

Loop = {
    "loop" ~ "(" ~ Identifier ~ ":" ~ Expression ~ ".." ~ Expression ~ ")" ~ Statement 
}

Node = {
    "node" ~ Identifier ~ ":" ~ Expression ~ ";"
}

//Identifiers
Identifier = @{
    (LETTER|"_") ~ (LETTER|NUMBER|"_")*
}

Target = {
    Identifier ~ ("[" ~ Expression ~ "]")? ~ ("." ~ Identifier ~ ("[" ~ Expression ~ "]")?)*
}

Number = ${
    ("s"|"u")? ~ "-"? ~ (HexNumber | OctNumber | BinNumber | DecNumber) ~ ("[" ~ Expression ~ "]")?
}

HexNumber = @{
    "x" ~ "-"? ~ ASCII_HEX_DIGIT+
}

OctNumber = @{
    "o" ~ "-"? ~ ASCII_OCT_DIGIT+
}

BinNumber = @{
    "b" ~ "-"? ~ ASCII_BIN_DIGIT+
}

DecNumber = @{
    "-"? ~ ASCII_DIGIT+
}

EnumVariant = ${
    Identifier ~ "::" ~ Identifier
}

//Types
Type = _{
    Vector |
    Signed |
    Unsigned |
    Clock |
    Reset |
    TypeBundle |
    EnumType
}

ConstantType = _{
    Signed |
    Unsigned
}

Vector = {
    "[" ~ Type ~ ":" ~ Expression ~ "]"
}

Unsigned = {
    "u[" ~ Expression ~ "]"
}

Signed = {
    "s[" ~ Expression ~ "]"
}

Clock = {
    "clock"
}

Reset = _{
    SyncReset | AsyncReset
}
SyncReset = {
    "reset"
}
AsyncReset = {
    "async_reset"
}

IOTypeBundle = {
    "(" ~ (InOut ~ Identifier ~ ":" ~ Type ~ ",")* ~ (InOut ~ Identifier ~ ":" ~ Type)? ~ ")"
}

InOut = {
    ("in" | "out")
}

TypeBundle = {
    "(" ~ (Flip? ~ Identifier ~ ":" ~ Type ~ ",")* ~ (Flip? ~ Identifier ~ ":" ~ Type)? ~ ")"
}

Flip = {
    "flip"
}

EnumType = {
    Identifier
}

//Expressions
Expression = {
    Comparison
}

Comparison = {
    Concatination ~ (ComparisonOperation ~ Concatination)*
}
ComparisonOperation = {
    "<"|">"|"<=" |">="|"=="|"!="
}

Concatination = {
    Logical ~ (ConcatinationOperation ~ Logical)*
}
ConcatinationOperation = {
    "|"
}

Logical = {
    Shift ~ (LogicalOperation ~ Shift)*
}
LogicalOperation = {
    "and"|"or"|"xor"
}

Shift = {
    Sum ~ (ShiftOperation ~ Sum)*
}
ShiftOperation = {
    "<<"|">>"
}

Sum = {
    Term ~ (SumOperation ~ Term)*
}
SumOperation = {
    "+"|"-"
}

Term = {
    Unary ~ (TermOperation ~ Unary)*
}
TermOperation = {
    "*"|"/"|"%"
}

Unary = {
    UnaryOperation* ~ Atom
}
UnaryOperation = {
    "!"|"-"
}

Atom = _{
    "(" ~ Expression ~ ")"|
    Function|
    Number|
    Target|
    ExpressionBundle
}

ExpressionBundle = {
    "(" ~ (Identifier ~ ":" ~ Expression ~ ",")* ~ (Identifier ~ ":" ~ Expression)? ~ ")"
}

Function = {
    (BuiltInFunction) ~ "(" ~ (Expression ~ ",")* ~ Expression? ~ ")"
}

BuiltInFunction = {
    "add"|"sub"|
    "mul"|"div"|"rem"|
    "lt"|"gt"|"leq"|"geq"|"eq"|"neq"|
    "pad"|
    "asUnsigned"|"asSigned"|"asClock"|"asReset"|"asAsyncReset"|
    "shl"|"shr"|"dshl"|"dshr"|
    "asSignedArithmatic"|
    "neg"|
    "not"|"and"|"or"|"xor"|
    "reduce_and"|"reduce_or"|"reduce_xor"|
    "cat"|"bits"|
    "head"|"tail"|
    "mux"
}